version = "1.0-SNAPSHOT"

plugins {
    id("com.google.cloud.tools.jib")
    alias(libs.plugins.spring.boot)
}

dependencies {
    // Generalized dependency bundles
    implementation(libs.bundles.base)
    implementation(libs.bundles.web)
    implementation(libs.bundles.serialization)
    implementation(libs.bundles.coroutines)
    implementation(libs.bundles.jwt)
    implementation(libs.bundles.caching)

    // Discord
    implementation(libs.jda)

    // Database
    implementation(libs.mongo)
    implementation(libs.nanoid)

    implementation(libs.chargebee)

    // Project
    implementation(project(":shared:core"))
}

jib {
    from {
        image = "amazoncorretto:25-al2023-headless"
    }

    to {
        image = "ghcr.io/${project.property("ghcrOrg")}/$name"
        tags = setOf(System.getenv("SEMAPHORE_GIT_SHA"), "latest")
        auth {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }

    container {
        jvmFlags = listOf(
            "-XX:+PrintCommandLineFlags",
            "-XshowSettings:vm",
//            "-XX:+PrintFlagsFinal",
//            "-Xlog:os+container=trace"
        )
    }
}

sentry {
    // Generates a JVM (Java, Kotlin, etc.) source bundle and uploads your source code to Sentry.
    // This enables source context, allowing you to see your source
    // code as part of your stack traces in Sentry.
    includeSourceContext = true

    org = "bot-astro"
    projectName = "central-api"
    authToken = System.getenv("SENTRY_AUTH_TOKEN")
}